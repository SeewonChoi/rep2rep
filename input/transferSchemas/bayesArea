import bayes
import areaDiagram
import transferSchemas/numNum

tSchema eventA:(bayesG,areaDiagramG) =
    source t:A:event
    target t':A:tag
    antecedent
    consequent ([t:A:event],[t':A:tag]) :: match
    strength 1

tSchema eventB:(bayesG,areaDiagramG) =
    source t:B:event
    target t':B:tag
    antecedent
    consequent ([t:B:event],[t':B:tag]) :: match
    strength 1

tSchema notEvent:(bayesG,areaDiagramG) =
    source t:event <- complement[t1:overline,t2:event]
    target t':tag <- reverseTag[t1':tag]
    antecedent ([t2:event],[t1':tag]) :: match
    consequent ([t:event],[t':tag]) :: match
    strength 1

tSchema constructEvent:(bayesG,areaDiagramG) =
    source t:probEqn <- prob[t1:events,t2:numExp]
    target t':area <- overlayRect[t1':empty,
                                  t2':rect <- cRect[t21':point <- cPoint[t211':0:numeral,t212':0:numeral],
                                                    t22':point <- cPoint[t221':numExp,t222':1:numeral]],
                                  t3':tag,
                                  t4':pattern]
    antecedent ([t1:events],[t3':tag]) :: match, ([t2:numExp],[t221':numExp]) :: equal
    consequent ([t:probEqn],[t':area]) :: encode
    strength 1

tSchema constructCond:(bayesG,areaDiagramG) =
    source t:probEqn <- prob[t1:condEvent <- condition[t11:event,t12:cond,t13:event],
                             t2:numExp]
    target t':area <- overlayRect[t1':area <- overlayRect[t11':empty,
                                                          t12':rect<- cRect[t121':point <- cPoint[t1211':0:numeral,t1212':0:numeral],
                                                                            t122':point <- cPoint[t1221':x:var,t1222':1:numeral]],
                                                          t13':tag,
                                                          t14':blue],
                                  t2':rect <- cRect[t21':point <- cPoint[t211':0:numeral,t212':0:numeral],
                                                    t22':point <- cPoint[t1221':x:var,t222':numExp]],
                                  t3':tag,
                                  t4':pattern]
    antecedent ([t13:event],[t13':tag]) :: match, ([t11:event],[t3':tag]) :: match, ([t2:numExp],[t222':numExp]) :: equal
    consequent ([t:probEqn],[t':area]) :: encode
    strength 1

tSchema constructInter:(bayesG,areaDiagramG) =
    source t:probEqn <- prob[t1:event <- infix[t11:event,t12:inter,t13:event],
                             t2:numExp]
    target t':area <- overlayRect[t1':area <- overlayRect[t11':empty,
                                                          t12':rect<- cRect[t121':point <- cPoint[t1211':0:numeral,t1212':0:numeral],
                                                                            t122':point <- cPoint[t1221':y:var,t1222':1:numeral]],
                                                          t13':tag,
                                                          t14':white],
                                  t2':rect <- cRect[t21':point <- cPoint[t211':0:numeral,t212':0:numeral],
                                                    t22':point <- cPoint[t1221':y:var,
                                                                         t222':numExp <- frac[t2221':numExp,t2222':div,t1221':y:var]]],
                                  t3':tag,
                                  t4':pattern]
    antecedent ([t11:event],[t13':tag]) :: match, ([t13:event],[t3':tag]) :: match, ([t2:numExp],[t2221':numExp]) :: equal
    consequent ([t:probEqn],[t':area]) :: encode
    strength 1

tSchema combineEqn:(bayesG,areaDiagramG) =
    source t:probSys <- addEqn[t1:probEqn,t2:probSys]
    target t':area <- combine[t1':area,t2':area]
    antecedent ([t1:probEqn],[t1':area]) :: encode, ([t2:probSys],[t2':area]) :: encode
    consequent ([t:probSys],[t':area]) :: encode
    strength 1

tSchema colourA:(bayesG,areaDiagramG) =
    source t:A:event
    target t':coloured <- colour[t1':empty,t2':A:tag,t3':green]
    antecedent
    consequent ([t:A:event],[t':coloured]) :: cMatch
    strength 1

tSchema colourB:(bayesG,areaDiagramG) =
    source t:B:event
    target t':coloured <- colour[t1':empty,t2':B:tag,t3':green]
    antecedent
    consequent ([t:B:event],[t':coloured]) :: cMatch
    strength 1

tSchema colourNot:(bayesG,areaDiagramG) =
    source t:event <- complement[t1:overline,t2:event]
    target t':coloured <- colour[t1':empty,
                                 t2':tag <- reverseTag[t21':tag],
                                 t3':green]
    antecedent ([t2:event],[t21':tag]) :: match
    consequent ([t:event],[t':coloured]) :: cMatch
    strength 1

tSchema colourCond:(bayesG,areaDiagramG) =
    source t:condEvent <- condition[t1:event,t2:cond,t3:event]
    target t':coloured <- colour[t1':coloured,t2':tag,t3':pattern]
    antecedent ([t3:event],[t1':coloured]) :: cMatch, ([t1:event],[t2':tag]) :: match
    consequent ([t:condEvent],[t':coloured]) :: cMatch
    strength 1

tSchema colourInter:(bayesG,areaDiagramG) =
    source t:event <- infix[t1:event,t2:inter,t3:event]
    target t':coloured <- colour[t1':coloured <- colour[t11':empty,t12':tag,t13':white],
                                 t2':tag,
                                 t3':green]
    antecedent ([t1:event],[t12':tag]) :: match, ([t3:event],[t2':tag]) :: match
    consequent ([t:event],[t':coloured]) :: cMatch
    strength 1

tSchema colourArea:(bayesG,areaDiagramG) =
    source t:probSys <- calculate[t1:probSys,t2:events]
    target t':area <- addColour[t1':area, t2':coloured]
    antecedent ([t1:probSys],[t1':area]) :: encode, ([t2:events],[t2':coloured]) :: cMatch
    consequent ([t:probSys],[t':area]) :: encode
    strength 1